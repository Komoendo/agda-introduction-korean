
# Equality
```agda
data _≡_ {A : Set} (a : A) : A → Set where
  refl : a ≡ a
```
a ≡ b는 "a와 b가 같다"라는 명제를 나타내는 타입이다. 여기서 '같다'란 a와 b가 정확히 같은 구문임을 의미한다.

## Reflective
```agda
a : (suc (suc zero)) ≡ (suc (suc zero))
a = refl

b : zero ≡ zero
b = refl
```
_≡_의 타입 정의에 의해 a ≡ a이다. (a는 자기자신과 같다) 이는 _≡_의 반사적(Reflective) 성질이다.

## Symmetric
```agda
sym : {A : Set} → {a b : A} → a ≡ b → b ≡ a
sym refl = refl
```
- 첫 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 두 변수 a와 b는 정확히 같은 구문이어야 한다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 a ≡ a 타입과 b ≡ a 타입도 같다.
- refl로 a ≡ a 타입의 값을 만들어 출력한다. 그럼 b ≡ a 타입의 값을 출력하는 것과 같다.

a ≡ b면 b ≡ a이다. 이는 _≡_의 대칭적(Symmetric) 성질이다.

## Transitive
```agda
trans : {A : Set} → {a b c : A} → a ≡ b → b ≡ c → a ≡ c
trans refl x = x
```
- 첫 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 두 변수 a와 b는 정확히 같은 구문이어야 한다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 a ≡ c 타입과 b ≡ c 타입도 같다.
- 두 번째 입력으로 들어온 b ≡ c 타입의 값을 출력한다. 그럼 a ≡ c 타입의 값을 출력하는 것과 같다.

a ≡ b고 b ≡ c면 a ≡ c이다. 이는 _≡_의 전이적(Transitive) 성질이다.

## Congruence
```agda
cong : {A B : Set} {a b : A} → (f : A → B) → a ≡ b → f a ≡ f b
cong f refl = refl
```
- 두 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 두 변수 a와 b는 정확히 같은 구문이어야 한다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 f a ≡ f a 타입과 f a ≡ f b 타입도 같다.
- refl로 f a ≡ f a 타입의 값을 출력한다. 그럼 f a ≡ f b 타입의 값을 출력하는 것과 같다.

a ≡ b면 f a ≡ f b이다.

# Equality 관련 예시 증명
\_≡\_ 타입을 활용하면 어떤 두 대상의 구문이 같음을 증명할 수 있다.

## 2 + 3 = 5
```agda
a : add (suc (suc zero)) (suc (suc (suc zero))) ≡ (suc (suc (suc (suc (suc zero)))))
a = refl
```
- add의 정의에 따라서 _≡_ 타입의 양 변의 값 5로 같다.
- 그래서 refl로 만들어낼 수 있다.

## n + 0 = n
```agda
n-plus-0-comm : (n : Nat) → add n zero ≡ n
n-plus-0-comm zero    = refl
n-plus-0-comm (suc n) = cong (λ x → suc x) (n-plus-0-comm n)
```
- zero 분기는 자명하다.
- suc 분기에서, cong으로 add n zero ≡ n의 양 변에 suc을 적용, add (suc n) zero ≡ suc n을 얻는다.

## n + (1 + m) = (1 + n) + m
```agda
plus-one : (n m : Nat) → add n (suc m) ≡ add (suc n) m
plus-one zero    m = refl
plus-one (suc n) m = cong (λ x → suc x) (plus-one n m)
```

## n + m = m + n
```agda
plus-comm : (n m : Nat) → add n m ≡ add m n
plus-comm zero    m = sym (n-plus-0-comm m)
plus-comm (suc n) m = trans (cong (λ x → suc x) (plus-comm n m)) (sym (plus-one m n))
```
드디어 a + b = b + a를 증명했다.

# 준비 완료!
이제 Agda로 수학을 하기 위한 도구들이 모두 갖춰졌다. 이제 Agda를 활용해서 수학기초론부터 사칙연산, 미적분, 위상수학 등 모든 수학을 형식화할 수 있다. 원한다면 물리학, 컴퓨터 과학 등 다른 학문의 연역적인 부분들을 형식화할 수도 있다.

이제 준비가 완료됐으니 이 가이드의 다음 챕터부터는 Agda로 수학적 대상들을 증명하는 예시들을 다룰 것이다. 참고로 다음 챕터부터는 Agda의 개념들을 친절하게 설명해주지는 않을 것이다. 기본적인 틀은 이미 모두 다뤘으므로, 나머지 모르는 개념들은 검색해 보면 이해할 수 있을 것이다.