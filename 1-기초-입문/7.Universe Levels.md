
# 러셀의 역설
$$P=\{자기자신을\space포함하지\space않는\space집합\}$$

$$P=\{x|x \notin x, x \space is \space set\}$$

자기자신을 포함하지 않는 집합들을 원소로 갖는 집합 P가 있다고 해 보자. P는 P의 원소일까?

- P ∈ P 라면: 그러면 P는 자기자신을 포함하는 집합이므로, P의 원소가 아니다. 즉 P ∉ P이다. 이는 P ∈ P라는 전제와 모순이다.
- P ∉ P 라면: 그러면 P는 자기자신을 포함하지 않는 집합이므로, P의 원소이다. 즉 P ∈ P이다. 이는 P ∉ P라는 전제와 모순이다.

그래서 P는 P의 원소인 것도 아니고, P의 원소가 아닌 것도 아니다. 모순이다!

이를 "러셀의 역설"이라고 부르며, 현대 수학은 집합을 정의하는 방법을 제한함으로서 P 같은 집합을 정의할 수 없도록 해 러셀의 역설을 해결한다. (즉 현대 수학에서 P와 같은 집합은 존재하지 않는다. 따라서 모순이 발생하지 않는다.)

## Agda에서 러셀의 역설?
Agda에서 타입이 집합 역할을 할 수 있음을 생각해보면, Agda에서도 러셀의 역설이 발생할 수 있지 않을까 상상해볼 수 있을 것이다.

하지만 Agda에서 러셀의 역설을 일으키는 타입은 정의할 수 없다. Agda도 현대 수학과 마찬가지로 이런 모순이 발생되는 것을 막기 때문이다.

# Universe Levels
자기자신을 포함하는 집합이 정의될 수 없다면 러셀의 역설은 발생하지 않을 것이다. Agda에서는 타입들에 level을 부여해, 모든 타입의 값은 그 타입보다 level이 낮은 타입만을 포함할 수 있도록 제한해 자기자신을 포함하는 집합 같은 것을 만들 수 없도록 한다. 이로서 모순을 막는다.

```agda
-- Level 0인 타입들
-- 지금까지 Set이라고 쓰던 것은 Set₀ 과 같다.
NatSet : Set₀
NatSet = Nat

NatNatProduct : Set₀
NatNatProduct = Nat × Nat

NatNatFunction : Set₀
NatNatFunction = Nat → Nat
```
지금까지 본 타입들은 모두 Level 0인 타입들이다. Level이 0인 타입의 값은, Level 0 타입의 값을 포함할 수 있다. 또는 값을 입력받아 값을 출력하는 함수를 포함할 수 있다.

```agda
record Magma : Set₁ where
  field
    Carrier : Set
    _∙_     : Carrier → Carrier → Carrier

DoubleNegationElimination : Set₁
DoubleNegationElimination = (A : Set) → (¬¬ A) → A

Type-of-Set-is-Set₁ : Set₁
Type-of-Set-is-Set₁ = Set

-- Magma를 이용하면, 타입과 그 타입에 관한 이항연산을 저장할 수 있다.
-- Magma 값 안에는 Level 0 타입이 저장되기 때문에, Magma는 Level 1 타입이다.
Nat-add-magma : Magma
Nat-add-magma = record
  { Carrier = Nat
  ; _∙_     = add
  }
```
Level 1인 타입의 값은, Level 0 타입을 포함할 수 있다. 또는 Level 0 타입을 입력받거나 출력하는 함수를 포함할 수 있다.

```agda
Type-of-Set₁-is-Set₂ : Set₂
Type-of-Set₁-is-Set₂ = Set₁

Type-of-Set₂-is-Set₃ : Set₃
Type-of-Set₂-is-Set₃ = Set₂

Type-of-Set₃-is-Set₄ : Set₄
Type-of-Set₃-is-Set₄ = Set₃
```
마찬가지로 Level n 타입이 존재한다.

# Level 산술
```agda
lzero : Level
lsuc  : (n : Level) → Level
_⊔_   : (n m : Level) → Level
```
`Agda.Primitive` 모듈에 있는 세 가지 연산을 이용해 Level을 연산할 수 있다. `lzero`는 0 level을 나타내고, `lsuc`으로는 level을 1 증가시킬 수 있다. `_⊔_`로는 두 level 중 더 큰 level을 얻을 수 있다.

## 예시: `_×_`
```agda
record _×_ (A : Set) (B : Set) : Set where
  field
    fst : A
    snd : B
```
앞서 본 `_×_` 타입 안에는 값만 담을 수 있다.

```agda
two-sets : Set × Set
two-sets = record
  { fst = Nat
  ; snd = Int
  }
```
타입을 담을 수가 없다.

```agda
open import Agda.Primitive using (Level; _⊔_)

record _×_ {l₁ l₂ : Level} (A : Set l₁) (B : Set l₂) : Set (l₁ ⊔ l₂) where
  field
    fst : A
    snd : B
```
`_×_`가 임의 Level의 타입의 값을 내부에 저장할 수 있도록 정의를 바꾸어주면 `_×_` 안에 타입을 담을 수 있다.
- `_×_`는 먼저 `l₁`과 `l₂` 두 Level을 입력받는다.
- 그리고 `l₁` Level의 타입 A, `l₂` Level의 타입 B를 입력받는다.
- `_×_` 내부에는 A 타입과 B 타입의 값이 저장되므로, `A × B` 타입의 Level은 A의 Level과 B의 Level 중 더 큰 Level이어야 한다. `_⊔_`를 이용해 `l₁`과 `l₂` 중 더 큰 값을 계산해 Level을 설정한다.

```agda
NatNatProduct : Set₀
NatNatProduct = Nat × Nat

a : NatNatProduct
a = record
  { fst = zero
  ; snd = zero
  }

NatSetProduct : Set₁
NatSetProduct = Nat × Set

b : NatSetProduct
b = record
  { fst = zero
  ; snd = Int
  }

SetSetProduct : Set₁
SetSetProduct = Set × Set

c : SetSetProduct
c = record
  { fst = Int
  ; snd = Nat
  }
```

## 예시: `_⊎_`
```agda
data _⊎_ {l₁ l₂ : Level} (A : Set l₁) (B : Set l₂) : Set (l₁ ⊔ l₂) where
  inj₁ : A → A ⊎ B
  inj₂ : B → A ⊎ B
```
`_⊎_`도 유사하게 확장해 정의할 수 있다.

## 예시: Principle of explosion
```agda
explode : {l : Level} {A : Set l} → ⊥ → A
explode ()
```
`⊥`로 임의 레벨 타입의 값을 만들어낼 수 있다.

# 참고
- [Universe Levels](https://agda.readthedocs.io/en/v2.6.3/language/universe-levels.html)
- Agda의 Level 기능을 꺼서, Agda에서 러셀의 역설을 구현하는 법: https://gist.github.com/jhrr/ec2254d3c9c61e706f68
  - {-#  OPTIONS --type-in-type #-} 구문으로 Agda의 Level 체크 기능을 꺼서, 러셀의 역설을 구현한다.
  - Level이 왜 필요한지 알 수 있다.