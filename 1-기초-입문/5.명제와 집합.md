# 타입이 명제
```
data Even : Nat → Set where
  even-zero  : Even zero
  even-plus2 : {n : Nat} → Even n → Even (suc (suc n))
```
앞서 본 Even 타입에 대해서 이런 말을 했었다. "Even n 타입의 값의 존재는, n이 짝수라는 것을 의미한다".
```agda
data NonZero : Nat → Set where
  nonzero-one   : NonZero (suc zero)
  nonzero-plus1 : {n : Nat} → NonZero n → NonZero (suc n)
```
또 NonZero 타입에 대해서는 이런 말을 했었다. "NonZero n 타입의 값의 존재는, n이 0이 아님을 의미한다".

공통점이 보인다. 어떤 타입의 값의 존재는, 어떤 명제가 참임을 의미한다.

|타입|대응되는 명제|
|---|---|
|Even n|n이 짝수다|
|NonZero n|n이 0이 아니다|

위 두 예제에서는 위와 같이 명제가 대응된다. 그리고 각 타입의 값의 존재는 각 명제가 참임을 의미한다.

## 타입 = 명제, 값 = 증명
|Agda|수학|
|---|---|
|타입|명제|
|값|증명|

타입과 값이 각각 대응되는 수학적 대상을 표로 나타내면 위와 같다. Agda의 타입은 수학에서의 명제에 대응되고, Agda의 값은 수학에서의 증명에 대응된다. 값이 증명이기 때문에, 값이 존재하면 명제가 참인 것이다.

## 값이 증명인 이유
값 자체가 어떻게 증명이 될 수 있는지에 대해서 의문이 들 수 있다. 값이 만들어지는 과정을 보면 실제로 값이 증명임을 알 수 있다.
```agda
6-is-even : Even (suc (suc (suc (suc (suc (suc zero))))))
6-is-even = even-plus2 (even-plus2 (even-plus2 even-zero))
```
위 코드는 "6이 짝수다"라는 명제를 나타내는 타입의 값이다. 이 값이 만들어지는 과정을 보자.
- constructor들: even-zero, even-plus2
- even-zero로 "0이 짝수다"라는 명제를 나타내는 타입의 값을 얻는다.
- even-plus2로 "0이 짝수다"라는 명제를 나타내는 타입의 값으로부터 "2가 짝수다" 라는 명제를 나타내는 타입의 값을 얻는다.
- even-plus2로 "2가 짝수다"라는 명제를 나타내는 타입의 값으로부터 "4가 짝수다" 라는 명제를 나타내는 타입의 값을 얻는다.
- even-plus2로 "4가 짝수다"라는 명제를 나타내는 타입의 값으로부터 "6이 짝수다" 라는 명제를 나타내는 타입의 값을 얻는다.

이건 다음과 굉장히 비슷하다.

- 공리들: 0은 짝수다. (n이 짝수면 2 + n도 짝수다)
- 0은 짝수다.
- (n이 짝수면 2 + n도 짝수다) 이고 0은 짝수이므로 2는 짝수다.
- (n이 짝수면 2 + n도 짝수다) 이고 2는 짝수이므로 4는 짝수다.
- (n이 짝수면 2 + n도 짝수다) 이고 4는 짝수이므로 6은 짝수다.

이는 "0은 짝수다.", "n이 짝수면 2 + n도 짝수다" 라는 가정이 있을 때 "6은 짝수다"의 증명이다.

값을 만들어내는 과정, 즉 값의 구조가 증명과정을 나타내는 것이다. 따라서 값 자체가 증명을 의미한다.

# A → B 타입의 명제: "A면 B이다"
타입이 명제를 의미하고 값이 증명을 의미한다면, A → B는 명제 A의 증명을 입력받아 명제 B의 증명을 출력하는 함수의 타입으로 생각할 수 있다. 이 타입은 "A면 B이다"라는 명제에 해당하며, 이 타입의 값(함수)는 "A면 B이다"에 대한 증명이다.

## 예시: n이 짝수이면 4 + n도 짝수다
```agda
f : {n : Nat} → Even n → Even (suc (suc (suc (suc n))))
f e = even-plus2 (even-plus2 e)
```
임의의 자연수 n에 대해, Even n 타입의 값이 들어오는 경우, Even (suc (suc (suc (suc n)))) 타입의 값을 출력한다.

```agda
-- 2는 짝수다. f에 따르면, 6도 짝수다.
a : Even (suc (suc (suc (suc (suc (suc zero))))))
a = f (even-plus2 even-zero)

-- 4는 짝수다. f에 따르면, 8도 짝수다.
b : Even (suc (suc (suc (suc (suc (suc (suc (suc zero))))))))
b = f (even-plus2 (even-plus2 even-zero))
```
수학에서 정리를 사용하는 것 처럼, 이 함수를 이용해 다른 명제를 증명할 수도 있다.

## 예시: n과 m이 짝수이면 n + m도 짝수다
```agda
f : {n m : Nat} → Even n → Even m → Even (add n m)
f even-zero           even-m = even-m
f (even-plus2 even-k) even-m = even-plus2 (f even-k even-m)
```
수학적 귀납법 같은 재귀적 증명이다.
```agda
[가정]
n이 0이다.
m이 짝수다.
[결론]
n + m = 0 + m = m이므로 n + m도 짝수다.
================================
[가정]
n = 2 + k이고 k와 n은 짝수다.
m이 짝수다.
[결론]
k가 짝수이고 m이 짝수이므로 k + m도 짝수다. (재귀호출, k가 n보다 작기 때문에 가능)
2 + (k + m)은 짝수다. (짝수에 대한 공리에 의해)
그런데 2 + (k + m) = (2 + k) + m = n + m 이므로 (정의에 의해)
n + m은 짝수다.
================================
n, m이 짝수일 때 가능한 모든 경우에 대해 증명했으므로,
모든 짝수 n, m에 대해 n + m은 짝수다.
```
수학 증명으로 보면 위와 같다.

# OR
```agda
data _⊎_ (A : Set) (B : Set) : Set where
  inj₁ : A → A ⊎ B
  inj₂ : B → A ⊎ B
```
Disjoint union 타입은 명제의 "또는" 을 나타내는 데에 사용할 수 있다. A와 B 중 원하는 타입의 값을 넣어서 A ⊎ B 타입의 값을 만들어낼 수 있다는 점을 이용하는 것이다.

## 예시: 자연수 n에 대해, "n은 짝수이다" 또는 "n은 홀수이다" 이다.
```agda
data Even : Nat → Set where
  even-zero  : Even zero
  even-plus2 : {n : Nat} → Even n → Even (suc (suc n))

data Odd : Nat → Set where
  odd-one  : Odd (suc zero)
  odd-plus2 : {n : Nat} → Odd n → Odd (suc (suc n))
```
먼저 Odd 타입을 추가로 정의해주자.
```agda
f : (n : Nat) → (Even n) ⊎ (Odd n)
f zero          = inj₁ even-zero
f (suc zero)    = inj₂ odd-one
f (suc (suc k)) = g (f k)
  where
    g : (Even k) ⊎ (Odd k) → (Even (suc (suc k))) ⊎ (Odd (suc (suc k)))
    g (inj₁ x) = inj₁ (even-plus2 x)
    g (inj₂ x) = inj₂ (odd-plus2 x)
```
n = 0, n = 1의 경우에 대해서 정의한 후, 2 이상의 수에 대해서는 재귀적으로 정의한다. `where`라는 새로운 구문이 등장했는데, 이 구문은 지역적으로 상수나 함수를 정의해놓고 그걸 사용할 수 있게 해 준다. 위 경우 "k가 짝수이거나 홀수이면, 2 + k도 짝수이거나 홀수이다" 를 의미하는 g 함수를 지역적으로 정의해놓고, f의 정의에 활용한다.

```agda
[가정]
n = 0
[결론]
정의에 의해 n은 짝수이다. 따라서 "n은 짝수이다" 또는 "n은 홀수이다" 이다.
================================
[가정]
n = 1
[결론]
정의에 의해 n은 홀수이다. 따라서 "n은 짝수이다" 또는 "n은 홀수이다" 이다.
================================
[가정]
n = 2 + k
[결론]
"k는 짝수이다" 또는 "k는 홀수이다" 이다. (재귀호출, k가 n보다 작기 때문에 가능)
g를 사용하면, "2 + k는 짝수이다" 또는 "2 + k"는 홀수이다" 이다.
2 + k = n이므로 "n은 짝수이다" 또는 "n은 홀수이다" 이다.
  [g]
    [가정]
    "k는 짝수이다" 또는 "k는 홀수이다" 이다.
    k는 짝수이다.
    [결론]
    2 + k는 짝수이다. (정의에 따라)
    따라서 "2 + k는 짝수이다" 또는 "2 + k는 홀수이다" 이다.
    ================================
    [가정]
    "k는 짝수이다" 또는 "k는 홀수이다" 이다.
    k는 홀수이다.
    [결론]
    2 + k는 홀수이다. (정의에 따라)
    따라서 "2 + k는 짝수이다" 또는 "2 + k는 홀수이다" 이다.
    ================================
    "k는 짝수이다" 또는 "k는 홀수이다" 일 때의 모든 경우에 대해 증명했으므로,
    "k는 짝수이다" 또는 "k는 홀수이다" 일 때 "2 + k는 짝수이다" 또는 "2 + k는 홀수이다" 이다.
================================
n이 자연수일 때 모든 경우에 대해 증명했으므로,
n이 자연수일 때 "n은 짝수이다" 또는 "n은 홀수이다" 이다.
```
수학적 증명은 위와 같다.

# AND
```agda
record _×_ (A : Set) (B : Set) : Set where
  field
    fst : A
    snd : B
```
Product 타입은 명제의 "그리고" 를 나타내는 데에 사용할 수 있다. A와 B 타입의 값 모두가 있어야 A × B 타입의 값을 만들어낼 수 있다는 점을 이용하는 것이다.

## 예시: 8의 배수 n에 대해, "n은 4의 배수이다" 그리고 "n은 2의 배수이다" 이다.
```agda
data Multiple-of-8 : Nat → Set where
  eight      : Multiple-of-8 (suc (suc (suc (suc (suc (suc (suc (suc zero))))))))
  plus-eight : {n : Nat} → Multiple-of-8 n → Multiple-of-8 (suc (suc (suc (suc (suc (suc (suc (suc n))))))))

data Multiple-of-4 : Nat → Set where
  four      : Multiple-of-4 (suc (suc (suc (suc zero))))
  plus-four : {n : Nat} → Multiple-of-4 n → Multiple-of-4 (suc (suc (suc (suc n))))

data Multiple-of-2 : Nat → Set where
  eight      : Multiple-of-2 (suc (suc zero))
  plus-eight : {n : Nat} → Multiple-of-2 n → Multiple-of-2 (suc (suc (suc n)))
```
먼저 "n은 8의 배수다", "n은 4의 배수다", "n은 2의 배수다" 명제를 나타내는 타입을 정의한다.
```agda
f : {n : Nat} → Multiple-of-8 n → (Multiple-of-4 n) × (Multiple-of-2 n)
f eight = record
  { fst = plus-four four
  ; snd = plus-two (plus-two (plus-two two))
  }
f { suc (suc (suc (suc (suc (suc (suc (suc k))))))) } (plus-eight k-is-multiple-of-8) = record
  { fst = plus-four (plus-four prev-four)
  ; snd = plus-two (plus-two (plus-two (plus-two prev-two)))
  }
  where
    prev : (Multiple-of-4 k) × (Multiple-of-2 k)
    prev = f k-is-multiple-of-8

    prev-four : Multiple-of-4 k
    prev-four = _×_.fst prev

    prev-two : Multiple-of-2 k
    prev-two = _×_.snd prev
```
eight의 경우를 증명하고, 16보다 큰 경우에 대해서는 재귀적으로 증명한다. (위 코드에서는 함수의 implicit 입력에 대해서 패턴매칭을 하고 있다.)
```agda
[가정]
n = 8이다.
n은 8의 배수이다.
[결론]
4는 4의 배수이다. 4 + 4 = 8은 4의 배수이다.
2는 2의 배수이다. 2 + 2 = 4는 2의 배수이다.
  2 + 4 = 6은 2의 배수이다.
  2 + 6 = 8은 2의 배수이다.
8 = n이다.
따라서 "n은 4의 배수이다" 그리고 "n은 2의 배수이다" 이다.
================================
[가정]
n = 8 + k이다.
n은 8의 배수이다.
k는 8의 배수이다.
[결론]
prev-four 에 따라 k는 4의 배수이다.
  4 + k는 4의 배수이다.
  4 + (4 + k) = 8 + k는 4의 배수이다.
  n = 8 + k 이므로 n은 4의 배수이다.
prev-two 에 따라 k는 2의 배수이다.
  2 + k는 2의 배수이다.
  2 + (2 + k) = 4 + k는 2의 배수이다.
  2 + (4 + k) = 6 + k는 2의 배수이다.
  2 + (6 + k) = 8 + k는 2의 배수이다.
  n = 8 + k 이므로 n은 2의 배수이다.
따라서 "n은 4의 배수이다" 그리고 "n은 2의 배수이다" 이다.
  [prev]
    "k는 4의 배수이다" 그리고 "k은 2의 배수이다" 이다. (재귀호출, k가 n보다 작기에 가능)
  [prev-four]
    (prev 로부터)
    k는 4의 배수이다.
  [prev-two]
    (prev 로부터)
    k는 2의 배수이다.
================================
n이 8의 배수일 때 가능한 모든 경우에 대해 증명했으므로
n이 8이 배수일 때, "n은 4의 배수이다" 그리고 "n은 2의 배수이다" 이다.
```
수학적 증명은 위와 같다.

# NOT과 0-type
```agda
-- 이 타입은 constructor가 없다.
data ⊥ : Set where
```
이 타입은 0-type이라고 불리는 타입이다. 이 타입은 constructor가 없고, 따라서 이 타입의 값은 만들어낼 수 없다. 값을 만들어낼 수 없는 이 타입은 쓸모없어보이지만, 명제의 부정을 나타내는 데에 유용하게 쓰일 수 있다. 예를 들어 어떤 타입 P에 대해 다음과 같은 타입을 생각해보자.
```agda
P → ⊥
```
이 타입의 값(즉 P 타입을 입력받아 ⊥ 타입의 값을 출력하는 함수)이 존재한다는 것은 무엇을 뜻할까? 그건 P도 ⊥와 같이 값을 만들어낼 수 없는 타입이라는 것이다. 만약 P 타입의 값이 존재한다면, 함수는 이 P 타입의 값에 대응되는 ⊥ 타입의 출력값을 내뱉어야 하는데, ⊥ 타입의 값은 만들어낼 방법이 없으므로 P → ⊥ 타입의 함수는 존재할 수 없기 때문이다.

그런데 한편, 값이 증명에 대응됨을 생각해보면, 값이 존재하지 않는다는 것은 증명이 존재하지 않는다는 것을 의미한다. 증명이 존재하지 않는다는 것을 명제가 거짓임으로 보면 "타입 P의 값을 만들어낼 수 없다"를 "P는 거짓이다"로 대응시킬수 있다. 즉 P → ⊥ 타입의 값의 존재는 P가 거짓임을 의미한다.

## 예시: 1은 짝수가 아니다.
```agda
f : (Even (suc zero)) → ⊥
f ()
```
함수 정의가 없다. 가능한 입력이 없기 때문이다.

어떤 원리로 이런 정의가 될 수 있을까? (Even (suc zero)) 타입의 pattern matching을 생각해보자. 이 타입의 값을 만드는 방법은.. 없다! Agda도 (Even (suc zero))라는 타입으로부터 이 사실을 알아낼 수 있으며, pattern matching에서 가능한 분기가 0개이므로 함수 본문이 한 개도 없이 함수를 정의할 수 있게 해 준다.

## 예시: n이 짝수라면 n은 홀수가 아니다.
```agda
f : {n : Nat} → Even n → Odd n → ⊥
f even-zero      ()
f (even-plus2 a) (odd-plus2 b) = f a b
```
- n이 zero인 경우에는 Odd zero를 만들 방법이 없으므로 함수 본문이 없다.
- n이 zero가 아닌 경우에는 n = 2 + k이다. f k로 재귀호출한다.

## 예시: P가 참이라면 Not P는 참이 아니다.
```agda
f : {P : Set} → P → (P → ⊥) → ⊥
f p z = z p
```

## ¬ 기호로 예쁘게 쓰기
```agda
¬_ : Set → Set
¬ P = P → ⊥
```
위와 같은 타입을 입력받아 타입을 출력하는 함수를 정의해두면, ¬ 기호로 부정을 예쁘게 쓸 수 있다.

```agda
f : ¬ Even (suc zero)
f ()
```
```agda
f : {n : Nat} → Even n → ¬ Odd n
f even-zero      ()
f (even-plus2 a) (odd-plus2 b) = f a b
```
```agda
f : {P : Set} → P → ¬ (¬ P)
f p z = z p
```

# Σ type
```agda
record Σ (A : Set) (B : A → Set) : Set where
  field
    fst : A
    snd : B fst
```
Σ 타입은 값과 그 값에 대한 증명을 같이 담아두는 용도로 사용한다. B는 A 타입의 값을 입력받아 타입을 출력하는 함수다. snd의 타입은 B fst로, fst의 값에 따라 바뀐다.

## 예시: Even인 자연수 2
```agda
a : Σ Nat (λ n → Even n)
a = record
  { fst = suc (suc zero)
  ; snd = even-plus2 even-zero
  }
```
fst에 들어가는 값이 2이므로, snd의 타입은 (λ n → Even n) 2의 값인 Even 2가 된다. 따라서 Even 2 값을 만들어서 snd에 넣는다.

a의 타입 `Σ Nat (λ n → Even n)`은 "짝수인 자연수"를 나타내는 타입으로 해석될 수 있다. 이 타입의 값에는, fst에는 자연수가 들어가 있고 snd에는 이 자연수가 짝수임에 대한 증명이 들어가 있다.

## 예시: Even인 자연수 0
```agda
a : Σ Nat (λ n → Even n)
a = record
  { fst = zero
  ; snd = even-zero
  }
```
fst에 저장되는 값이 2이므로, snd의 타입은 (λ n → Even n) 0의 값인 Even 0가 된다.

a의 타입 `Σ Nat (λ n → Even n)`은 "짝수인 자연수"를 나타내는 타입으로 해석될 수 있다. 이 타입의 값에는, fst에는 자연수가 들어가 있고 snd에는 이 자연수가 짝수임에 대한 증명이 들어가 있다.

## 예시: 합이 짝수인 두 자연수
```agda
a : Σ Nat (λ n → Σ Nat (λ m → Even (add n m)))
a = record
  { fst = suc zero
  ; snd = record
    { fst = suc (suc (suc zero))
    ; snd = even-plus2 (even-plus2 even-zero)
    }
  }
```
Σ 여러개를 중첩해서 쓰면 여러 값을 저장하고, 여러 값에 대한 명제를 저장할 수 있다.

```agda
record Nat-pair-sum-even : Set where
  field
    fst : Nat
    snd : Nat
    sum-even : Even (add fst snd)

a : Nat-pair-sum-even
a = record
  { fst = suc zero
  ; snd = suc (suc (suc zero))
  ; sum-even = even-plus2 (even-plus2 even-zero)
  }
```
하지만 이렇게 표현하는 것이 더 깔끔하다.

Σ 타입은 간단한 경우에 사용하고, 보통 값과 명제의 개수가 많은 경우에는 이렇게 record를 따로 만든다.

# 타입이 집합
|타입|대응되는 명제|
|---|---|
|Even n|n이 짝수다|
|NonZero n|n이 0이 아니다|

위에서 이런 표를 봤다.

|명제|진리집합|진리집합에 관한 표현으로 표현한 명제|
|---|---|---|
|n이 짝수다|짝수인 자연수의 집합|n이 (짝수인 자연수의 집합)의 원소다|
|n이 0이 아니다|0이 아닌 자연수의 집합|n이 (0이 아닌 자연수의 집합)의 원소다|

위 표에 있던 명제들에 대한 진리집합을 생각해보자. 그리고 진리집합에 대한 명제로 다시 바꾸어 써 보자.

|타입|대응되는 명제|대응되는 집합론적 명제|
|---|---|---|
|Even n|n이 짝수다|n이 (짝수인 자연수의 집합)의 원소다|
|NonZero n|n이 0이 아니다|n이 (0이 아닌 자연수의 집합)의 원소다|

이제 원래 테이블에 연결을 해 보자.

|타입|대응되는 집합|
|---|---|
|Even|짝수인 자연수의 집합|
|NonZero|0이 아닌 자연수의 집합|

타입을 집합처럼 생각할 수 있다.

일반적으로, indexed datatype은 집합처럼 생각할 수 있다.

## 예시: 짝수인 자연수의 집합
Even은 짝수인 자연수의 집합으로 볼 수 있다.
```agda
-- 2는 (짝수의 집합)의 원소다.
a : Even (suc (suc zero))
a = even-plus2 even-zero

-- 0은 (짝수의 집합)의 원소다.
b : Even zero
b = even-zero
```

## 예시: 홀수인 자연수의 집합
Odd는 홀수인 자연수의 집합으로 볼 수 있다.
```agda
-- 3은 (홀수의 집합)의 원소다.
a : Odd (suc (suc (suc zero)))
a = odd-plus2 odd-one

-- 1은 (홀수의 집합)의 원소다.
b : Odd (suc zero)
b = odd-one
```

## 예시: (n, 2 + n)의 집합
```agda
-- Plus2는 (n, 2 + n)의 집합이다.
-- Plus2 = { (n, 2 + n) | n ∈ ℕ }
data Plus2 : Nat → Nat → Set where
  plus2 : {n : Nat} → Plus2 n (suc (suc n))

-- (0, 2)는 ((n, 2 + n)의 집합)의 원소다.
a : Plus2 zero (suc (suc zero))
a = plus2

-- (1, 3)은 ((n, 2 + n)의 집합)의 원소다.
b : Plus2 (suc zero) (suc (suc (suc zero)))
b = plus2

-- (a, b)가 ((n, 2 + n)의 집합)의 원소이면, (1 + a, 1 + b)도 ((n, 2 + n)의 집합)의 원소이다.
f : {a b : Nat} → Plus2 a b → Plus2 (suc a) (suc b)
f {a} {suc (suc a)} plus2 = plus2
```