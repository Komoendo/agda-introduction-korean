# 고전적 수학에서 가능하지만 Agda에서 불가능한 것
## Double Negation Elimination
고전적 논리학에서 명제 P에 대해, ¬¬P이면 P이다. 이를 Double Negation Elimination이라고 이야기한다. 그런데 Agda에서 Double Negation Elimination을 나타내는 함수를 만들고자 하면:
```agda
f : {P : Set} → ¬ (¬ P) → P
f = ?
```
만들 수 없다. 이런 함수를 만들 수 없는 이유는, Agda에서 값을 만들기 위해서는 그 타입의 constructor를 이용해서 값을 만들어내야 하는데, 함수가 입력받은 ¬ (¬ P) 타입의 값은 P 타입의 값을 만들어내는 과정에 대한 정보를 전혀 제공해주지 않기 때문이다. ¬ (¬ P) 타입의 값은 "P 타입의 값이 없다는 것은 거짓이다" 라는 것을 알려줄 뿐이다.

이는 Agda에서는 수학에서의 귀류법을 사용할 수 없음을 의미한다. 귀류법은 P가 거짓임을 가정했을 때 모순이 발생한다는 것을 보임으로서 P가 거짓이 아님을 보이고, Double Negation Elimination으로 P를 얻어내 최종적으로 P를 증명하는 증명 방법이다. 그런데 Agda에서는 Double Negation Elimination이 불가능하기 때문에 귀류법을 사용할 수 없다.

## Law of excluded middle
고전적 논리학에서 모든 명제는 참 또는 거짓이다. 참인지 거짓인지 결정할 수 없을 수는 있지만, 참이거나 거짓이거나 둘 중 하나라는 것은 참이다. 그런데 이걸 Agda에서 만들려고 하면:
```agda
f : (A : Set) → A ⊎ (¬ A)
f = ?
```
불가능하다. 이것도 Double Negation Elimination과 비슷한 이유 때문인데, A 타입의 값과 (¬ A) 타입의 값 중에서 A 타입의 값을 만들어야 하는 경우라면 A 타입의 값을 만들어낼 방법을 알 수 없기 때문이다.

# 가능하게 만들기
그런데 이 둘을 사용할 수 없으면, 고전적 수학에서 이루어진 증명들 중 많은 수의 증명들을 사용할 수 없다. 그리고 이 둘이 없는 경우 증명이 불가능한 명제들도 있다. 그렇기 때문에 만약 Agda에서 고전적 수학을 흉내내고자 하는 경우에는 이 둘을 사용할 수 있도록 할 필요가 있다.

Agda에서 고전적 수학을 흉내낼 때 사용되는 대표적인 두 가지 방법은 다음과 같다.

- Double negation된 타입의 값을 증명으로 보기
- LEM(Law of Excluded Middle)을 가정 하에 나온 값을 증명으로 보기


# [첫번째 방법] Double negation된 타입의 값을 증명으로 보기
```agda
(P → ⊥) → ⊥
```
위 타입의 값의 존재는, P 타입의 값이 존재하지 않음이 거짓임을 의미한다. 즉 P 타입의 값이 존재한다는 것, P 의 증명이 존재한다는 것을 의미한다. 그럼 이를 P가 참임을 의미하는 것으로 볼 수 있지 않을까? P에 대한 정확한 증명을 알지는 못하지만, P가 참이라고 말하기에는 충분해보인다.

|Agda|수학|
|---|---|
|타입 P|명제 P|
|P 타입의 값|P의 증명|
|(P → ⊥) → ⊥ 타입의 값|P의 증명|

만약 이를 P가 참임을 의미하는 것으로 받아들이면 위와 같은 대응표가 만들어진다. 그냥 P 타입의 값도 여전히 P의 증명이 될 수 있는 이유는, P 타입의 값으로 (P → ⊥) → ⊥ 타입의 값을 만들 수 있기 때문이다.

```agda
¬¬_ : Set → Set
¬¬ P = (P → ⊥) → ⊥
```

편의를 위해 Double negation된 타입을 만들어주는 함수를 만들 수 있다.

|Agda|수학|
|---|---|
|타입 P|명제 P|
|P 타입의 값|P의 증명|
|¬¬ P 타입의 값|P의 증명|

정의한 기호로 다시 쓰면 이렇다.

## Double Negation Elimination
```agda
double-neg-elim : {P : Set} → ¬ (¬ P) → ¬¬ P
double-neg-elim x = x
```
새로운 표에 따르면, "¬¬P 일때 P이다" 를 의미하는 함수를 만들 수 있다. Double Negation Elimination이다.

## Law of excluded middle
```agda
law-of-excluded-middle : (P : Set) → ¬¬ (P ⊎ (¬ P))
law-of-excluded-middle P a = a (inj₂ (λ x → a (inj₁ x)))
```
"P 또는 ¬P이다"를 의미하는 함수도 만들 수 있다. Law of excluded middle이다.

# Double negation을 편하게 쓸 수 있게 하기
새로운 '증명'의 정의를 사용하면 고전적 대상들을 증명할 수 있긴 하다. 그런데, 증명하는 과정은 끔찍하다.

예를 들어서 Double negation 꼴인 다른 함수 f : P → ¬¬Q, g : Q → ¬¬R을 활용해서 새로운 타입 ¬¬S를 증명하고 싶다고 해 보자. 그리고 S의 증명에는 ¬¬R이 핵심적인 역할을 한다고 가정하자. 어찌저찌 P를 얻어서, f에 입력하면 (Q → ⊥) → ⊥를 얻는다. 이제 g에 Q를 넣어야 하는데, (Q → ⊥) → ⊥에서 Q를 만들 수가 없기 때문에, 빙빙 돌아 ¬¬R를 얻어내야 한다.

이런 귀찮음을 해결하기 위해서는 다음과 같은 함수가 필요하다.
```agda
¬¬ P → P
```
그런데 앞서 봤듯, 이 함수는 있을 수가 없다!

하지만 이것과 비슷한 효과를 주는 함수를 만들 수는 있다.

## Double negation monad
```agda
_>>=_ : {A B : Set} → ¬¬ A → (A → ¬¬ B) → ¬¬ B
a >>= f = λ z₁ → a (λ z₂ → f z₂ z₁)

pure : {A : Set} → A → ¬¬ A
pure a = λ f → f a
```

`>>=` 함수를 이용하면 A를 입력받아 ¬¬ B를 만드는 함수(A → ¬¬ B)를 작성하고, ¬¬ A를 넣으면 ¬¬ B를 얻을 수 있다.

### 예시: (A면 B, B면 C)라면 (A면 C)이다.
```agda
f : {A B C : Set} → (A → ¬¬ B) → (B → ¬¬ C) → (A → ¬¬ C)
f a-to-b b-to-c a = 
  a-to-b a >>= λ b →
  b-to-c b
```
`a-to-b a`로 `¬¬ B`를 만들고, 뒤에 `B → ¬¬ C`함수를 넣어서 `¬¬ C`를 얻는다.

### 예시: (A면 B, B면 C, C면 D, D면 E)라면 (A면 E)이다.
```agda
f : {A B C D E : Set} → (A → ¬¬ B) → (B → ¬¬ C) → (C → ¬¬ D) → (D → ¬¬ E) → (A → ¬¬ E)
f a-to-b b-to-c c-to-d d-to-e a = 
  a-to-b a >>= λ b →
  b-to-c b >>= λ c →
  c-to-d c >>= λ d →
  d-to-e d
```
연속해서 사용하면 위와 같이 사용할 수 있다.

## Do notation
Agda는 [Do-notation](https://agda.readthedocs.io/en/v2.6.3/language/syntactic-sugar.html#do-notation)이라는 편의기능을 제공해 >>=를 활용한 표현을 간결하게 쓸 수 있도록 해 준다.

```agda
do x ← m
   m'
```
위와 같은 구문을 작성하면
```
m >>= λ x →
m'
```
이런 구문으로 자동으로 Agda가 해석한다.

### 예시: (A면 B, B면 C)라면 (A면 C)이다.
```agda
f : {A B C : Set} → (A → ¬¬ B) → (B → ¬¬ C) → (A → ¬¬ C)
f a-to-b b-to-c a = do 
  b ← a-to-b a
  b-to-c b
```

### 예시: (A면 B, B면 C, C면 D, D면 E)라면 (A면 E)이다.
```agda
f : {A B C D E : Set} → (A → ¬¬ B) → (B → ¬¬ C) → (C → ¬¬ D) → (D → ¬¬ E) → (A → ¬¬ E)
f a-to-b b-to-c c-to-d d-to-e a = do
  b ← a-to-b a
  c ← b-to-c b
  d ← c-to-d c
  d-to-e d
```
구문의 생김새를 보면 `←` 표시가 ¬¬을 제거하는 역할을 하는 것 처럼 보인다.(그냥 생김새가 그래보인다는 것이다)

## Proof by case + Law of excluded middle
```agda
tf-case : {A B : Set} → (A → ¬¬ B) → (¬ A → ¬¬ B) → ¬¬ B
tf-case {A} {B} f g = do
  a-or-¬a ← law-of-excluded-middle A
  h a-or-¬a
  where
    h : (A ⊎ (¬ A)) → ¬¬ B
    h (inj₁ a) = f a
    h (inj₂ ¬a) = g ¬a
```
A → B이고, ¬ A → B라면, B이다.

(A를 가정했을 때 B가 참임)과 (not A를 가정했을 때 B가 참임)을 보이면 B가 참임을 보일 수 있다.

## Triple negation ellimination
```agda
¬¬¬-elim : {A : Set} → ¬¬ (¬ A) → ¬ A
¬¬¬-elim a = λ z₁ → a (λ z₂ → z₂ z₁)
```
어떤 명제의 부정을 증명하고자 할 때 유용하게 쓰일 수 있다.

# 함수의 Double negation
Double negation이 고전적 수학의 불완전한 흉내인 이유가 여기에 있다.

고전적 수학에서의 (a : A) → B a ((a : A)면 B a이다)라는 명제는 두 가지 타입으로 표현할 수 있다.

```agda
f : ¬¬ ((a : A) → B a)
g : ((a : A) → ¬¬ (B a))
```
두 타입은 고전적 수학의 관점에서 모두 같다.

하지만 Agda에서 ¬¬로 고전적 수학을 불완전하게 **흉내**낸 관점에서 이들이 갖는 의미는 다르다. `¬¬ ((a : A) → B a)`가 `((a : A) → ¬¬ (B a))`보다 강력하다. (즉 타입 A, B에 대해 f일때 g임은 증명할 수 있지만 g 일때 f임은 증명할 수 없다) 강력한 명제는 더 증명이 **어렵다**.

그렇다면 늘 더 쉬운 형태(g)로 증명하면 되는 것 아닌가? 문제는 데이터타입 같은 곳 안에 함수가 들어있는 경우이다. 데이터타입 같은 곳 안에 함수가 들어있는 경우, 아무 생각 없이 데이터타입에 ¬¬를 붙이고 ¬¬ ((a : A) → B a)를 증명하려고 삽질을 하기 쉽다.

```agda
data _≤_ : Nat → Nat → Set where
  ≤-zero : {n : Nat}   → zero ≤ n
  ≤-suc  : {n m : Nat} → n ≤ m → (suc n) ≤ (suc m)

record Min (P : Nat → Set) (n : Nat) : Set where
  field
    min  : (m : Nat) → P m → n ≤ m
    in-P : P n
```
예를 들어, "P가 n을 최솟값으로 갖는다" 라는 명제를 나타내는 Min P n 타입을 생각해보자.

```agda
¬¬ (Min P n)
```
Min P n을 고전적 수학으로 증명하려고 할 때 어떻게 해야 할까? 무심코 위와 같은 타입을 증명하고자 할 수 있다. 하지만 이 타입의 값을 만드는 과정은 `¬¬ ((m : Nat) → P m → n ≤ m)` 타입의 증명을 요구할 것이다. 그리고 이런 타입의 증명은 앞에서 말했듯 **어렵다**.

그래서 만약 Min을 고전적 수학으로 증명하고자 한다면 Min의 ¬¬ 버전 타입을 따로 만들어줄 필요가 있다.

```agda
record ¬¬Min (P : Nat → Set) (n : Nat) : Set where
  field
    min  : (m : Nat) → P m → ¬¬ (n ≤ m)
    in-P : ¬¬ (P n)
```
이런 타입을 만들고, `¬¬Min P n`을 증명하면 된다. 고전적 수학의 관점에서는 `¬¬ (Min P n)`을 증명하는 것과 `¬¬Min P n`을 증명하는 것은 같다. 하지만 Agda에서는 둘의 난이도가 다르기 때문에 더 난이도가 낮은 `¬¬Min P n`을 증명한다.

# [두 번째 방법] LEM(Law of Excluded Middle)을 가정 하에 나온 값을 증명으로 보기
Double negation을 이용하면 큰 공리 없이 고전적 수학을 Agda에서 표현할 수 있다는 장점이 있지만, 위의 `Min`과 `¬¬Min`의 예시에서 알 수 있듯이 단순히 어떤 타입에 `¬¬`를 붙이는 것으로 '고전적으로 증명가능함'의 표현이 불가능해서 매번 알맞는 형태의 타입을 만들어줘야하는 귀찮음이 있다.

대신, "모든 타입 `P`에 대해, 반드시 `P` 타입의 값 또는 `¬ P` 타입의 값을 얻을 수 있다"라는 가정, 즉 Law of excluded middle의 가정 하에 명제를 증명하는 함수를 증명으로 받아들이면 이런 귀찮음 없이 고전적 수학을 흉내낼 수 있다. 또 >>= 같은 것이 필요없고 자유롭다.

```agda
data Dec (P : Set) : Set where
  yes : ( p :   P) → Dec P
  no  : (¬p : ¬ P) → Dec P

LEM : Set₁
LEM = (P : Set) → Dec P
```
LEM은 "모든 타입 `P`에 대해, 반드시 `P` 타입의 값 또는 `¬ P` 타입의 값을 얻을 수 있다"라는 명제를 나타낸다. (Dec 타입 정의 참고)

|Agda|수학|
|---|---|
|타입 P|명제 P|
|P 타입의 값|P의 증명|
|LEM → P 타입의 값|P의 증명|

LEM 가정 하에 명제를 증명하는 함수를 증명으로 보겠다는 것은, 위와 같은 대응을 받아들이겠다는 것이다.

## Double negation elimination
```agda
LEM-¬¬-elim : LEM → {P : Set} → (¬¬ P) → P
LEM-¬¬-elim lem {P} = g (lem P)
  where
  g : Dec P → (¬¬ P) → P
  g (yes p) _ = p
  g (no ¬p) p = explode (p ¬p)
```
LEM은 Double negation elimination과 동치이다. LEM 가정 하에서는, Double negation elimination이 가능하다. 따라서 LEM 가정 하에서 어떤 명제 `P`를 증명하고자 하면, 자유롭게 Double negation을 제거하며 `P`를 증명할 수 있다.

## 예시: "Nat → Bool 함수는 true를 출력하는 점을 갖거나, 늘 false를 출력한다."
```agda
case_of_ : {A : Set} {B : Set} → A → (A → B) → B
case x of f = f x

g : LEM → (f : Nat → Bool) → ((n : Nat) → f n ≡ false) ⊎ (Σ Nat (λ n → f n ≡ true))
g lem f = case (lem (Σ Nat (λ n → f n ≡ true))) of
  λ { (yes p) → inj₂ p
    ; (no ¬p) → inj₁ λ n →
        case (lem (f n ≡ true)) of 
        λ { (yes q) → explode (¬p (record { fst = n ; snd = q }))
          ; (no ¬q) → h ¬q
          }
    }
  where
  h : {a : Bool} → ¬ (a ≡ true) → a ≡ false
  h {true}  b = explode (b refl)
  h {false} b = refl
```
분기 처리를 편리하게 하기 위한 `case_of_`라는 함수를 만들어서 활용했다.

lem을 활용해서 "true를 출력하는 점을 갖는다"와, "true를 출력하는 점이 없다" 둘 중 하나의 값을 얻는다. true를 출력하는 점을 갖는 경우, 그 점을 그대로 출력한다.(`inj₂ p`) "true를 출력하는 점이 없다" 인 경우, 모든 n에 대해서 f n ≡ false를 출력하는 함수를 만든다.

```agda
(f : Nat → Bool) → ((n : Nat) → f n ≡ false) ⊎ (Σ Nat (λ n → f n ≡ true))
```
원래 위와 같은 타입의 함수는 못 만든다. 왜냐하면 f가 모든 점에서 false를 출력하는지, 또는 함숫값이 true인 어떤 점을 갖는지 유한한 계산을 통해 알아낼 수 없기 때문이다. 하지만 LEM 가정 하에서는 이런 함수를 만들 수 있다.

# Agda 내 고전적 수학 표현의 활용 예시: 자연수의 Well-ordering principle
Well-ordering principle이란 공집합이 아닌 임의의 자연수 부분집합 P에 대해, 최솟값인 자연수가 들어있다는 정리이다. 예를 들어..
- {3, 4, 7}에서는 최솟값 3이 있다.
- {10, 2, 6}에서는 최솟값 2가 있다.
- {7}에서는 최솟값 7이 있다.

이 정리에 따르면 수학에서는 자연수 부분집합을 입력받아 그 집합 안에 들어있는 최솟값을 출력하는 함수(함수관계)를 만들 수 있다. Agda에서는 최솟값을 출력하는 함수를 만들수는 없지만, 최솟값이 존재한다는 증명을 출력하는 함수를 만들 수 있다.

구현은 WellOrderingPrinciple.agda를 참고하라.

# 참고
- [Classical Mathematics for a Constructive World](https://arxiv.org/ftp/arxiv/papers/1008/1008.1213.pdf)