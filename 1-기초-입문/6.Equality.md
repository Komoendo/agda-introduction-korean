# Term
Term은 값의 표기이다.

## 예시: 4
```agda
a : Nat
a = suc (suc (suc (suc zero)))

b : Nat
b = a

c : Nat
c = add (suc (suc zero)) (suc (suc zero))

d : Nat
d = add (suc (suc (suc zero))) (suc zero)
``` 
a, b, c, d는 4 라는 같은 값을 서로 다른 term으로 표현한다.

- a는 `suc (suc (suc (suc zero)))` 라는 term으로 4를 표현한다.
- b는 `a` 라는 term으로 4를 표현한다.
- c는 `add (suc (suc zero)) (suc (suc zero))` 라는 term으로 4를 표현한다.
- d는 `add (suc (suc (suc zero))) (suc zero)` 라는 term으로 4를 표현한다.

# Definitional Equality
Agda는 내부적으로 Definitional Equality에 관련된 추론 규칙들을 통해 서로 다른 term이 나타내는 대상이 같음을 알아낼 수 있다. (예를 들어, 위 예시에서 a, b, c, d는 모두 다른 term이지만 Agda는 이들이 모두 같음을 안다.) Definitional Equality가 작동하는 방법을 알면, Agda가 어떤 것을 내부적으로 '같다'고 판단할 수 있는지 알 수 있다.

두 term a, b에 대해 a = b로 "두 Term a, b는 definitionally equal하다"를 나타낸다.

## 예시: 상수 정의
```agda
a : Nat
a = suc zero
```
위와 같은 상수 정의는 다음과 같은 definitional equality의 존재를 함의한다.

```
a = suc zero
```
상수 정의 구문 자체가 definitional equality를 의미한다고 생각할 수 있다.

## 예시: 함수 정의
```agda
add : Nat → Nat → Nat
add zero    b = b
add (suc a) b = suc (add a b)
```
위와 같은 함수 정의는 다음과 같은 definitional equality의 존재를 함의한다.

```
(a, b는 Nat)
add zero b    = b
add (suc a) b = suc (add a b)
```
함수 정의 구문 자체가 definitional equality를 의미한다고 생각할 수 있다.

## 예시: add zero (suc (suc zero)) = (suc (suc zero))
```
add zero b = b
```
위 definitional equality에서, b에 (suc (suc zero))를 대입하면
```
add zero (suc (suc zero)) = (suc (suc zero)) 
```
를 얻을 수 있다.

## 예시: suc (suc (suc (suc zero))) = add (suc (suc zero)) (suc (suc zero))
```
add (suc (suc zero)) (suc (suc zero))
= suc (add (suc zero) (suc (suc zero)))
= suc (suc (add zero (suc (suc zero))))
```
add의 두 번째 definitional equaltiy를 이용해, 앞선 예시와 비슷한 과정으로 유도할 수 있다.
```
suc (suc (add zero (suc (suc zero))))
= suc (suc (suc (suc zero)))
```
그런데 앞서 `add zero (suc (suc zero)`에 대한 definitional equality를 보였으므로 위와 같은 결론을 얻을 수 있다.

## 예시: add zero n = n
```
add zero n
= n
```
add의 정의에 따라서다.

## 예시: add n zero = n 은 Agda가 알아낼 수 없음
```
add n zero
```
이런 형태에 알맞는 add의 definitional equality 정의가 없다.(첫 번째 인자가 suc인지 zero인지 모를 때에 대한 definitional equality 정의는 없다.) 그래서 add n zero = n 은 agda가 알아낼 수 없다.

# Propositional Equality
```agda
data _≡_ {A : Set} (a : A) : A → Set where
  refl : a ≡ a
```
a ≡ b는 "a와 b가 같다"라는 명제를 나타내는 타입이다. a ≡ b 타입의 값의 존재는 a = b임을 의미한다.(a ≡ b 타입의 값은 refl로밖에 생성할 수 없고, 그렇다면 a = b일 수 밖에 없다.) 이 타입을 이용하면 Agda가 알지 못하는 definitional equality에 대해서도 증명을 할 수 있다.

## Reflective
```agda
a : (suc (suc zero)) ≡ (suc (suc zero))
a = refl

b : zero ≡ zero
b = refl
```
_≡_의 타입 정의에 의해 a ≡ a이다. (a는 자기자신과 같다) 이는 _≡_의 반사적(Reflective) 성질이다.

## Symmetric
```agda
sym : {A : Set} → {a b : A} → a ≡ b → b ≡ a
sym refl = refl
```
- 첫 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 a = b이다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 a ≡ a 타입과 b ≡ a 타입도 같다.
- refl로 a ≡ a 타입의 값을 만들어 출력한다. 그럼 b ≡ a 타입의 값을 출력하는 것과 같다.

a ≡ b면 b ≡ a이다. 이는 _≡_의 대칭적(Symmetric) 성질이다.

## Transitive
```agda
trans : {A : Set} → {a b c : A} → a ≡ b → b ≡ c → a ≡ c
trans refl x = x
```
- 첫 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 a = b이다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 a ≡ c 타입과 b ≡ c 타입도 같다.
- 두 번째 입력으로 들어온 b ≡ c 타입의 값을 출력한다. 그럼 a ≡ c 타입의 값을 출력하는 것과 같다.

a ≡ b고 b ≡ c면 a ≡ c이다. 이는 _≡_의 전이적(Transitive) 성질이다.

## Congruence
```agda
cong : {A B : Set} {a b : A} → (f : A → B) → a ≡ b → f a ≡ f b
cong f refl = refl
```
- 두 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 a = b이다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 f a ≡ f a 타입과 f a ≡ f b 타입도 같다.
- refl로 f a ≡ f a 타입의 값을 출력한다. 그럼 f a ≡ f b 타입의 값을 출력하는 것과 같다.

a ≡ b면 f a ≡ f b이다.

# Propositional Equality 관련 예시 증명
`_≡_` 타입을 활용하면 어떤 두 대상이 같음을 증명할 수 있다.

## 2 + 3 ≡ 5
```agda
a : add (suc (suc zero)) (suc (suc (suc zero))) ≡ (suc (suc (suc (suc (suc zero)))))
a = refl
```
- Agda는 Definitional equality에 따라 좌항과 우항이 같음을 안다.
- 따라서 refl로 만들어낼 수 있다. (좌항과 우항이 같기 때문에 a ≡ a 형태다)

## n + 0 ≡ n
```agda
n-plus-0-comm : (n : Nat) → add n zero ≡ n
n-plus-0-comm zero    = refl
n-plus-0-comm (suc n) = cong (λ x → suc x) (n-plus-0-comm n)
```
- zero 분기는 자명하다. (add zero zero = zero 임을 Agda가 안다)
- suc 분기에서, cong으로 add n zero ≡ n의 양 변에 suc을 적용, add (suc n) zero ≡ suc n을 얻는다.
- n + 0 = n는 Agda가 모르지만(그냥 refl로 증명 불가능), Propositional Equality를 이용하면 귀납적으로 유도할 수 있다.

## n + (1 + m) ≡ (1 + n) + m
```agda
plus-one : (n m : Nat) → add n (suc m) ≡ add (suc n) m
plus-one zero    m = refl
plus-one (suc n) m = cong (λ x → suc x) (plus-one n m)
```

## n + m = m + n
```agda
plus-comm : (n m : Nat) → add n m ≡ add m n
plus-comm zero    m = sym (n-plus-0-comm m)
plus-comm (suc n) m = trans (cong (λ x → suc x) (plus-comm n m)) (sym (plus-one m n))
```
드디어 a + b = b + a를 증명했다.

# Equivalence relation
앞서 알아본 `_≡_`는 두 대상이 구문적인 면에서 같은지에 대한 명제였다. 그런데 두 대상의 구문이 다르더라도 "같다"고 말하고 싶은 경우가 있다. 예를 들어 1/2와 2/4는 표현되는 구문이 다르지만, 같다고 이야기한다. 이럴 때에는 원하는 형태의 "같음"(Equivalence relation)을 직접 정의해주어야 한다. 이때 Equivalence relation은 Reflective, symmetric, transitive 성질을 가져야 한다.

## 예시: Frac
```agda
record Frac : Set where
  field
    numerator     : Int
    denominator-1 : Nat
```
앞에서 했던 유리수의 정의. 같은 유리수가 서로 다른 형태로 표현될 수 있다.
```agda
1/2 : Frac
1/2 = record
  { numerator     = pos (suc zero)
  ; denominator-1 = (suc zero)
  }

2/4 : Frac
2/4 = record
  { numerator     = pos (suc (suc zero))
  ; denominator-1 = (suc (suc (suc zero)))
  }

-- 1/2과 2/4는 같지 않아~!
a : ¬ (1/2 ≡ 2/4)
a ()
```
1/2과 2/4를 나타내는 구문은 다르므로, `_≡_`의 기준에서는 다르다.
```agda
-- a/b에 c을 곱해 (a*c)/(b*c) 형태로 만들어주는 함수.
frac-inflate : Frac → Nat → Frac
frac-inflate
  record
  { numerator     = a
  ; denominator-1 = b-1
  }
  c-1
  = record
  { numerator     = int-multiply a (pos (suc c-1))
  ; denominator-1 = minus-1 (multiply (suc b-1) (suc c-1))
  }
```
먼저, 분모와 분자에 같은 자연수를 곱해주는 함수를 만든다.

```agda
record _≡f_ (a : Frac) (b : Frac) : Set where
  field
    common : Frac
    a-m    : Nat
    b-m    : Nat
    a-eq   : frac-inflate common a-m ≡ a
    b-eq   : frac-inflate common b-m ≡ b
```
그리고 equivalence relation을 위와 같이 정의한다. 이 정의가 뜻하는 것은, 어떤 두 유리수 a, b에 대해서, common 이라는 유리수 하나가 존재해서, 이 common을 적절히 변형(분모와 분자에 같은 수를 곱하는 것)해 a와 b를 각각 만들어낼 수 있다면 이 두 유리수는 같다는 것이다. 기약분수의 개념을 생각해보면, 이 정의는 두 유리수의 같음을 잘 나타냄을 알 수 있다.

```agda
a : 1/2 ≡f 2/4
a = record
  { common = record { numerator = pos (suc zero) ; denominator-1 = (suc zero) }
  ; a-m    = zero
  ; b-m    = (suc zero)
  ; a-eq   = refl
  ; b-eq   = refl
  }
```
이제 `_≡f_`를 활용해 1/2와 2/4가 같음을 증명할 수 있다.

`_≡f_`는 Reflective, Symmetric, Transitive한 성질을 지니지만 증명은 생략하겠다.