
# Equality
```agda
data _≡_ {A : Set} (a : A) : A → Set where
  refl : a ≡ a
```
a ≡ b는 "a와 b가 같다"라는 명제를 나타내는 타입이다. 여기서 '같다'란 a와 b가 정확히 같은 구문임을 의미한다.

## Reflective
```agda
a : (suc (suc zero)) ≡ (suc (suc zero))
a = refl

b : zero ≡ zero
b = refl
```
_≡_의 타입 정의에 의해 a ≡ a이다. (a는 자기자신과 같다) 이는 _≡_의 반사적(Reflective) 성질이다.

## Symmetric
```agda
sym : {A : Set} → {a b : A} → a ≡ b → b ≡ a
sym refl = refl
```
- 첫 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 두 변수 a와 b는 정확히 같은 구문이어야 한다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 a ≡ a 타입과 b ≡ a 타입도 같다.
- refl로 a ≡ a 타입의 값을 만들어 출력한다. 그럼 b ≡ a 타입의 값을 출력하는 것과 같다.

a ≡ b면 b ≡ a이다. 이는 _≡_의 대칭적(Symmetric) 성질이다.

## Transitive
```agda
trans : {A : Set} → {a b c : A} → a ≡ b → b ≡ c → a ≡ c
trans refl x = x
```
- 첫 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 두 변수 a와 b는 정확히 같은 구문이어야 한다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 a ≡ c 타입과 b ≡ c 타입도 같다.
- 두 번째 입력으로 들어온 b ≡ c 타입의 값을 출력한다. 그럼 a ≡ c 타입의 값을 출력하는 것과 같다.

a ≡ b고 b ≡ c면 a ≡ c이다. 이는 _≡_의 전이적(Transitive) 성질이다.

## Congruence
```agda
cong : {A B : Set} {a b : A} → (f : A → B) → a ≡ b → f a ≡ f b
cong f refl = refl
```
- 두 번째 입력을 pattern matching하면 refl 분기만 나온다.
- 그런데 refl이라면 두 변수 a와 b는 정확히 같은 구문이어야 한다. (constructor 정의에 따라)
- 즉 a와 b는 같다. 따라서 f a ≡ f a 타입과 f a ≡ f b 타입도 같다.
- refl로 f a ≡ f a 타입의 값을 출력한다. 그럼 f a ≡ f b 타입의 값을 출력하는 것과 같다.

a ≡ b면 f a ≡ f b이다.

# Equality 관련 예시 증명
\_≡\_ 타입을 활용하면 어떤 두 대상의 구문이 같음을 증명할 수 있다.

## 2 + 3 = 5
```agda
a : add (suc (suc zero)) (suc (suc (suc zero))) ≡ (suc (suc (suc (suc (suc zero)))))
a = refl
```
- add의 정의에 따라서 _≡_ 타입의 양 변의 값 5로 같다.
- 그래서 refl로 만들어낼 수 있다.

## n + 0 = n
```agda
n-plus-0-comm : (n : Nat) → add n zero ≡ n
n-plus-0-comm zero    = refl
n-plus-0-comm (suc n) = cong (λ x → suc x) (n-plus-0-comm n)
```
- zero 분기는 자명하다.
- suc 분기에서, cong으로 add n zero ≡ n의 양 변에 suc을 적용, add (suc n) zero ≡ suc n을 얻는다.

## n + (1 + m) = (1 + n) + m
```agda
plus-one : (n m : Nat) → add n (suc m) ≡ add (suc n) m
plus-one zero    m = refl
plus-one (suc n) m = cong (λ x → suc x) (plus-one n m)
```

## n + m = m + n
```agda
plus-comm : (n m : Nat) → add n m ≡ add m n
plus-comm zero    m = sym (n-plus-0-comm m)
plus-comm (suc n) m = trans (cong (λ x → suc x) (plus-comm n m)) (sym (plus-one m n))
```
드디어 a + b = b + a를 증명했다.

# Equivalence relation
앞서 알아본 `_≡_`는 두 대상의 구문이 같은지에 대한 명제였다. 그런데 두 대상의 구문이 다르더라도 "같다"고 말하고 싶은 경우가 있다. 예를 들어 1/2와 2/4는 표현된 구문이 다르지만, 같다고 이야기한다. 이럴 때에는 원하는 형태의 "같음"(Equivalence relation)을 직접 정의해주어야 한다. 이때 Equivalence relation은 Reflective, symmetric, transitive 성질을 가져야 한다.

## 예시: Frac
```agda
record Frac : Set where
  field
    numerator     : Int
    denominator-1 : Nat
```
앞에서 했던 유리수의 정의. 같은 유리수가 서로 다른 형태로 표현될 수 있다.
```agda
1/2 : Frac
1/2 = record
  { numerator     = pos (suc zero)
  ; denominator-1 = (suc zero)
  }

2/4 : Frac
2/4 = record
  { numerator     = pos (suc (suc zero))
  ; denominator-1 = (suc (suc (suc zero)))
  }

-- 1/2과 2/4는 같지 않아~!
a : ¬ (1/2 ≡ 2/4)
a ()
```
1/2과 2/4를 나타내는 구문은 다르므로, `_≡_`의 기준에서는 다르다.
```agda
-- a/b에 c을 곱해 (a*c)/(b*c) 형태로 만들어주는 함수.
frac-inflate : Frac → Nat → Frac
frac-inflate
  record
  { numerator     = a
  ; denominator-1 = b-1
  }
  c-1
  = record
  { numerator     = int-multiply a (pos (suc c-1))
  ; denominator-1 = minus-1 (multiply (suc b-1) (suc c-1))
  }
```
먼저, 분모와 분자에 같은 자연수를 곱해주는 함수를 만든다.

```agda
record _≡f_ (a : Frac) (b : Frac) : Set where
  field
    common : Frac
    a-m    : Nat
    b-m    : Nat
    a-eq   : frac-inflate common a-m ≡ a
    b-eq   : frac-inflate common b-m ≡ b
```
그리고 equivalence relation을 위와 같이 정의한다. 이 정의가 뜻하는 것은, 어떤 두 유리수 a, b에 대해서, common 이라는 유리수 하나가 존재해서, 이 common을 적절히 변형(분모와 분자에 같은 수를 곱하는 것)해 a와 b를 각각 만들어낼 수 있다면 이 두 유리수는 같다는 것이다. 기약분수의 개념을 생각해보면, 이 정의는 두 유리수의 같음을 잘 나타냄을 알 수 있다.

```agda
a : 1/2 ≡f 2/4
a = record
  { common = record { numerator = pos (suc zero) ; denominator-1 = (suc zero) }
  ; a-m    = zero
  ; b-m    = (suc zero)
  ; a-eq   = refl
  ; b-eq   = refl
  }
```
이제 `_≡f_`를 활용해 1/2와 2/4가 같음을 증명할 수 있다.

`_≡f_`는 Reflective, Symmetric, Transitive한 성질을 지니지만 증명은 생략하겠다.